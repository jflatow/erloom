#!/usr/bin/env escript
%% -*- erlang -*-

follow(Path) ->
    case file:read_link_all(Path) of
        {ok, Linked} ->
            follow(filename:absname(Linked, filename:dirname(Path)));
        {error, einval} ->
            Path
    end.

add_paths() ->
    Script = follow(escript:script_name()),
    RelPath = filename:dirname(filename:dirname(Script)),
    code:add_pathsa([RelPath ++ "/ebin",
                     RelPath ++ "/deps/erlkit/ebin",
                     RelPath ++ "/../erlkit/ebin"]).

get_opts(Args) ->
    get_opts(Args, {[], []}).

get_opts(["-n", Num|Rest], {Args, Opts}) ->
    get_opts(Rest, {Args, [{limit, util:int(Num)}|Opts]});
get_opts(["-r"|Rest], {Args, Opts}) ->
    get_opts(Rest, {Args, [{direction, reverse}|Opts]});

get_opts(["-d"|Rest], {Args, Opts}) ->
    get_opts(Rest, {Args, [{deps, true},
                           {refs, true}|Opts]});
get_opts(["-t"|Rest], {Args, Opts}) ->
    get_opts(Rest, {Args, [{time, {unix, at}}|Opts]});
get_opts(["-y"|Rest], {Args, Opts}) ->
    get_opts(Rest, {Args, [{yarn, true}|Opts]});
get_opts(["-P"|Rest], {Args, Opts}) ->
    get_opts(Rest, {Args, [{prior, true}|Opts]});
get_opts([Arg|Rest], {Args, Opts}) ->
    get_opts(Rest, {[Arg|Args], Opts});
get_opts([], {Args, Opts}) ->
    {lists:reverse(Args), lists:reverse(Opts)}.

main(Args) ->
    add_paths(),
    case get_opts(Args) of
        {Homes, Opts} ->
            lists:foldl(fun (H, _) -> lcat(H, Opts) end, nil, Homes)
    end.

lcat(Home, Opts) ->
    {Dir, O1} = util:pop(Opts, direction, forward),
    {Limit, O2} = util:pop(O1, limit),
    LocusFmt = erloom_bin:locus_formatter(O2),
    MessageFmt = erloom_bin:message_formatter(O2),
    erloom_logs:fold(
      fun ({Locus, Message},  _) ->
              io:format("~s\t~s~n", [LocusFmt(Locus), MessageFmt(Message)])
      end, nil, #{home => Home, direction => Dir, limit => Limit}).
